// Задача groups: Группы повторяющихся элементов
// Дана последовательность L из N целых чисел. Необходимо оставить в последовательности только один элемент из каждой группы идущих подряд одинаковых чисел.

// В данной задаче запрещается использовать массивы. Для хранения последовательности необходимо использовать односвязные списки. Для решения задачи опишите нерекурсивную процедуру groups(L), которая в списке L из каждой группы подряд идущих равных элементов оставляет один.

// Формат входных данных
// В первой строке вводится целое число N — количество элементов последовательности L ( 0 ≤ N ≤ 1000 ).
// Во второй строке вводится N целых чисел — элементы последовательности L ( |Li| ≤ 32000 ).

// Формат результата
// Вывести в одну строку последовательность L после преобразования.

type
    List = ^TNode;
    TNode = record
        data: integer;
        next: List;
    end;

procedure print(L: List);
{ Выводит список }
begin
    if L = nil then
        exit;
    write(L^.data, ' ');
    print(L^.next);
end;

procedure input(var L: List; n: integer);
{ Создаёт список из n элементов }
begin
    if n = 0 then
    begin
        L := nil;
        exit;
    end;
    new(L);
    read(L^.data);
    input(L^.next, n - 1);
end;

procedure clear(var L: List);
{ Очищает список }
begin
    if L = nil then
        exit;
    clear(L^.next);
    dispose(L);
end;

procedure groups(var L: List);
{ Удаляет повторяющиеся элементы в группе }
var
    p, q: List;
begin
    if L = nil then
        exit;

    {Инициализация указателей:
        p - указатель на текущий элемент
        q - указатель на следующий элемент
    }
    p := L;
    q := L^.next;

    // Пока не конец списка
    while q <> nil do
    begin
        // Если элементы равны, то удаляем q
        if p^.data = q^.data then
        begin
            // Сдвигаем указатель
            p^.next := q^.next;

            // Очищаем память
            dispose(q);

            // Сдвигаем указатель
            q := p^.next;
        end
        else
        begin
            // Сдвигаем указатели
            p := q;
            q := q^.next;
        end;
    end;
end;

var
    L: List;
    n: integer;

begin
    // Заполнение списка
    read(n);
    input(L, n);

    // Удаление повторяющихся элементов
    groups(L);

    // Вывод результата
    print(L);

    // Очистка памяти
    clear(L);
end.
